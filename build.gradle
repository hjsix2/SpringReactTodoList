buildscript {
    ext {
        springBootVersion = '1.3.3.RELEASE'
        frontendRoot = 'frontend'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "com.moowork.node" version "0.11"
}

node {
    version = '6.4.0'
    download = true
    nodeModulesDir = file("${project.projectDir}/${frontendRoot}/")
}

task cleanPublic(type: Delete) {
    delete './public'
}

task buildFrontend(type: NpmTask, dependsOn: cleanPublic) {
    workingDir frontendRoot
    args = ['run', 'build']
    
    inputs.files file("${frontendRoot}")
    outputs.files file("${frontendRoot}/build")
}

task copyFrontendToPublic(type: Copy, dependsOn: buildFrontend) {
    from "${frontendRoot}/build"
    into "public" 
}

group 'org.kelog'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    runtime('com.h2database:h2')
}

bootRun.dependsOn copyFrontendToPublic
jar.dependsOn copyFrontendToPublic

clean.dependsOn cleanPublic


jar.into('public') {
    from('public')
}
